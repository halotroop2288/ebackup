//file:noinspection GroovyAssignabilityCheck
plugins {
	id 'java'
	id 'java-library'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'com.github.johnrengelman.shadow'

	group = rootProject.maven_group
	version = rootProject.version

	// Ensures all versions have access to common code
	sourceSets {
		main {
			java {
				if (project != project(":common")) {
					srcDir project(':common').file('src/main/java')
				}
			}
		}
	}

	def targetJavaVersion = project.javaVersion as int

	java {
		def javaVersion = JavaVersion.toVersion(targetJavaVersion)
		sourceCompatibility = javaVersion
		targetCompatibility = javaVersion
		if (JavaVersion.current() < javaVersion) {
			toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
		}
	}

	tasks.withType(JavaCompile).configureEach {
		if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
			options.release.set targetJavaVersion
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		if (project != project(":common")) {
			api project(":common")
		}
		compileOnly('org.jetbrains:annotations:' + project.annotations_version)
		shadow implementation('com.cronutils:cron-utils:' + project.cron_utils_version)
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}
	}
}
